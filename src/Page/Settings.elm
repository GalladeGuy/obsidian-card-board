module Page.Settings exposing
    ( Msg(..)
    , dialogs
    , update
    )

import BoardConfig exposing (BoardConfig)
import FeatherIcons
import Flip
import Html exposing (Html)
import Html.Attributes exposing (class, placeholder, selected, type_, value)
import Html.Events exposing (onClick, onInput)
import InteropPorts
import Model exposing (Model)
import Parser
import SafeZipper exposing (SafeZipper)
import SettingsEditState exposing (SettingsEditState)
import TagBoard



-- UPDATE


type Msg
    = AddBoardClicked
    | AddBoardConfirmed
    | BoardTypeSelected String
    | DeleteBoardRequested
    | DeleteBoardConfirmed
    | EnteredCompletedCount String
    | EnteredNewBoardTitle String
    | EnteredTags String
    | EnteredTitle String
    | ModalCancelClicked
    | ModalCloseClicked
    | SettingsBoardNameClicked Int
    | ToggleIncludeOthers
    | ToggleIncludeUndated
    | ToggleIncludeUntagged


update : Msg -> Model -> ( Model, Cmd Msg )
update msg model =
    case msg of
        AddBoardClicked ->
            ( Model.mapSetingsEditState SettingsEditState.moveToAdd model, Cmd.none )

        AddBoardConfirmed ->
            ( Model.mapSetingsEditState SettingsEditState.confirmAdd model, Cmd.none )

        BoardTypeSelected boardType ->
            ( Model.mapBoardBeingAdded (BoardConfig.updateBoardType boardType) model, Cmd.none )

        DeleteBoardRequested ->
            ( Model.mapSetingsEditState SettingsEditState.moveToDelete model, Cmd.none )

        DeleteBoardConfirmed ->
            ( Model.mapSetingsEditState SettingsEditState.confirmDelete model, Cmd.none )

        EnteredCompletedCount value ->
            ( Model.mapBoardBeingEdited (BoardConfig.updateCompletedCount (String.toInt value)) model, Cmd.none )

        EnteredNewBoardTitle title ->
            ( Model.mapBoardBeingAdded (BoardConfig.updateTitle title) model, Cmd.none )

        EnteredTags tags ->
            ( Model.mapBoardBeingEdited (BoardConfig.updateTags tags) model, Cmd.none )

        EnteredTitle title ->
            ( Model.mapBoardBeingEdited (BoardConfig.updateTitle title) model, Cmd.none )

        ModalCancelClicked ->
            closeDialogOrExit model

        ModalCloseClicked ->
            closeDialogOrExit model

        SettingsBoardNameClicked index ->
            ( Model.mapSetingsEditState (SettingsEditState.changeBoardBeingEdited index) model, Cmd.none )

        ToggleIncludeOthers ->
            ( Model.mapBoardBeingEdited BoardConfig.toggleIncludeOthers model, Cmd.none )

        ToggleIncludeUndated ->
            ( Model.mapBoardBeingEdited BoardConfig.toggleIncludeUndated model, Cmd.none )

        ToggleIncludeUntagged ->
            ( Model.mapBoardBeingEdited BoardConfig.toggleIncludeUntagged model, Cmd.none )


closeDialogOrExit : Model -> ( Model, Cmd Msg )
closeDialogOrExit model =
    let
        ( newState, cancelAction ) =
            SettingsEditState.cancelCurrentState model.settingsEditState
    in
    case cancelAction of
        SettingsEditState.ExitWithConfig config ->
            ( Model.mapSetingsEditState (always newState) model, InteropPorts.updateSettings config )

        SettingsEditState.ExitWithNoConfig ->
            ( Model.mapSetingsEditState (always newState) { model | boardConfigs = SafeZipper.empty }
            , Cmd.batch
                [ InteropPorts.updateSettings SafeZipper.empty
                , InteropPorts.closeView
                ]
            )

        SettingsEditState.SetToState ->
            ( Model.mapSetingsEditState (always newState) model, Cmd.none )



-- VIEW


dialogs : SettingsEditState -> Html Msg
dialogs editState =
    case editState of
        SettingsEditState.AddingBoard configsBeingEdited newConfig ->
            Html.div []
                [ modalSettingsView configsBeingEdited
                , modalAddBoard newConfig
                ]

        SettingsEditState.DeletingBoard configsBeingEdited ->
            Html.div []
                [ modalSettingsView configsBeingEdited
                , modalConfirmDelete
                ]

        SettingsEditState.EditingBoard configsBeingEdited ->
            modalSettingsView configsBeingEdited

        SettingsEditState.NotBeingEdited ->
            Html.text ""


modalAddBoard : BoardConfig -> Html Msg
modalAddBoard newConfig =
    Html.div [ class "modal-container" ]
        [ Html.div [ class "modal-bg" ] []
        , Html.div [ class "modal" ]
            [ Html.div
                [ class "modal-close-button"
                , onClick ModalCloseClicked
                ]
                []
            , Html.div [ class "modal-title" ]
                [ Html.text "Add new board" ]
            , Html.div [ class "modal-form" ]
                [ Html.div [ class "form-item" ]
                    [ Html.div [ class "form-item-name" ]
                        [ Html.text "Title" ]
                    , Html.div [ class "form-item-control" ]
                        [ Html.input
                            [ type_ "text"
                            , value <| BoardConfig.title newConfig
                            , onInput EnteredNewBoardTitle
                            ]
                            []
                        ]
                    ]
                , Html.div [ class "form-item" ]
                    [ Html.div [ class "form-item-name" ]
                        [ Html.text "Type" ]
                    , Html.div [ class "form-item-control" ]
                        [ Html.select
                            [ class "dropdown"
                            , onInput BoardTypeSelected
                            ]
                            [ Html.option
                                [ value "dateBoard"
                                , selected <| BoardConfig.isForDateBoard newConfig
                                ]
                                [ Html.text "Date board" ]
                            , Html.option
                                [ value "tagBoard"
                                , selected <| BoardConfig.isForTagBoard newConfig
                                ]
                                [ Html.text "Tag board" ]
                            ]
                        ]
                    ]
                , Html.div [ class "dialog-buttons" ]
                    [ Html.button
                        [ onClick <| ModalCancelClicked
                        ]
                        [ Html.text "Cancel"
                        ]
                    , Html.button
                        [ class "mod-cta"
                        , onClick <| AddBoardConfirmed
                        ]
                        [ Html.text "Add"
                        ]
                    ]
                ]
            ]
        ]


modalConfirmDelete : Html Msg
modalConfirmDelete =
    Html.div [ class "modal-container" ]
        [ Html.div [ class "modal-bg" ] []
        , Html.div [ class "modal" ]
            [ Html.div
                [ class "modal-close-button"
                , onClick ModalCloseClicked
                ]
                []
            , Html.div [ class "modal-title" ]
                [ Html.text "Confirm Deletion" ]
            , Html.div [ class "dialog-buttons" ]
                [ Html.button
                    [ onClick <| ModalCancelClicked
                    ]
                    [ Html.text "Do not delete"
                    ]
                , Html.button
                    [ class "mod-warning"
                    , onClick <| DeleteBoardConfirmed
                    ]
                    [ Html.text "Go ahead"
                    ]
                ]
            ]
        ]


modalSettingsView : SafeZipper BoardConfig -> Html Msg
modalSettingsView configs =
    Html.div [ class "modal-container" ]
        [ Html.div [ class "modal-bg" ] []
        , Html.div [ class "modal mod-settings" ]
            [ Html.div
                [ class "modal-close-button"
                , onClick ModalCloseClicked
                ]
                []
            , Html.div [ class "modal-content" ]
                [ Html.div [ class "settings-menu vertical-tab-header" ]
                    (Html.div [ class "vertical-tab-header-group-title" ]
                        [ Html.text "Boards"
                        , Html.div
                            [ class "vertical-tab-header-group-title-icon"
                            , onClick AddBoardClicked
                            ]
                            [ FeatherIcons.plus
                                |> FeatherIcons.withSize 1
                                |> FeatherIcons.withSizeUnit "em"
                                |> FeatherIcons.toHtml []
                            ]
                        ]
                        :: (configs
                                |> SafeZipper.indexedMapSelectedAndRest settingTitleSelectedView settingTitleView
                                |> SafeZipper.toList
                           )
                    )
                , settingsFormView <| SafeZipper.current configs
                ]
            ]
        ]


settingsFormView : Maybe BoardConfig -> Html Msg
settingsFormView boardConfig =
    case boardConfig of
        Just (BoardConfig.DateBoardConfig config) ->
            let
                includeUndatedStyle =
                    if config.includeUndated then
                        " is-enabled"

                    else
                        ""
            in
            Html.div [ class "settings-form-container" ]
                [ Html.div [ class "settings-form" ]
                    [ Html.div [ class "setting-item" ]
                        [ Html.div [ class "setting-item-info" ]
                            [ Html.div [ class "setting-item-name" ]
                                [ Html.text "Title" ]
                            , Html.div [ class "setting-item-description" ]
                                [ Html.text "The name of this board" ]
                            ]
                        , Html.div [ class "setting-item-control" ]
                            [ Html.input
                                [ type_ "text"
                                , value config.title
                                , onInput EnteredTitle
                                ]
                                []
                            ]
                        ]
                    , Html.div [ class "setting-item" ]
                        [ Html.div [ class "setting-item-info" ]
                            [ Html.div [ class "setting-item-name" ]
                                [ Html.text "Include Undated" ]
                            , Html.div [ class "setting-item-description" ]
                                [ Html.text "Whether to include a column for tasks with no due date" ]
                            ]
                        , Html.div [ class "setting-item-control" ]
                            [ Html.div
                                [ class <| "checkbox-container" ++ includeUndatedStyle
                                , onClick ToggleIncludeUndated
                                ]
                                []
                            ]
                        ]
                    , Html.div [ class "setting-item" ]
                        [ Html.div [ class "setting-item-info" ]
                            [ Html.div [ class "setting-item-name" ]
                                [ Html.text "Completed Count" ]
                            , Html.div [ class "setting-item-description" ]
                                [ Html.text "How many completed tasks to show.  Set to zero to disable the completed column altogether." ]
                            ]
                        , Html.div [ class "setting-item-control" ]
                            [ Html.input
                                [ type_ "text"
                                , value <| String.fromInt config.completedCount
                                , onInput EnteredCompletedCount
                                ]
                                []
                            ]
                        ]
                    , Html.div [ class "setting-item dialog-buttons" ]
                        [ Html.button
                            [ class "mod-warning"
                            , onClick <| DeleteBoardRequested
                            ]
                            [ Html.text "Delete this board"
                            ]
                        ]
                    ]
                ]

        Just (BoardConfig.TagBoardConfig config) ->
            let
                includeOthersStyle =
                    if config.includeOthers then
                        " is-enabled"

                    else
                        ""

                includeUntaggedStyle =
                    if config.includeUntagged then
                        " is-enabled"

                    else
                        ""

                tagText =
                    config.columns
                        |> List.map (\c -> "#" ++ c.tag ++ " " ++ c.displayTitle)
                        |> String.join "\n"
            in
            Html.div [ class "settings-form-container" ]
                [ Html.div [ class "settings-form" ]
                    [ Html.div [ class "setting-item" ]
                        [ Html.div [ class "setting-item-info" ]
                            [ Html.div [ class "setting-item-name" ]
                                [ Html.text "Title" ]
                            , Html.div [ class "setting-item-description" ]
                                [ Html.text "The name of this board" ]
                            ]
                        , Html.div [ class "setting-item-control" ]
                            [ Html.input
                                [ type_ "text"
                                , value config.title
                                , onInput EnteredTitle
                                ]
                                []
                            ]
                        ]
                    , Html.div [ class "setting-item" ]
                        [ Html.div [ class "setting-item-info" ]
                            [ Html.div [ class "setting-item-name" ]
                                [ Html.text "Columns" ]
                            , Html.div [ class "setting-item-description" ]
                                [ Html.div []
                                    [ Html.text "The tags to use to define board columns." ]
                                , Html.div []
                                    [ Html.text
                                        ("Each line should be a tag followed by the column heading.  "
                                            ++ "Add a trailing / to the tag to include tasks with any subtags in the column too.  "
                                            ++ "If you do not specify the heading it will be auto-generated from the tag."
                                        )
                                    ]
                                ]
                            ]
                        , Html.div [ class "setting-item-control" ]
                            [ Html.textarea
                                [ onInput EnteredTags
                                , placeholder "#tag1 Column heading\n#tag2/\n#tag3/subtag\ntag4"
                                ]
                                [ Html.text tagText ]
                            ]
                        ]
                    , Html.div [ class "setting-item" ]
                        [ Html.div [ class "setting-item-info" ]
                            [ Html.div [ class "setting-item-name" ]
                                [ Html.text "Include Others" ]
                            , Html.div [ class "setting-item-description" ]
                                [ Html.text "Whether to include a column for tasks with tags other than those specified" ]
                            ]
                        , Html.div [ class "setting-item-control" ]
                            [ Html.div
                                [ class <| "checkbox-container" ++ includeOthersStyle
                                , onClick ToggleIncludeOthers
                                ]
                                []
                            ]
                        ]
                    , Html.div [ class "setting-item" ]
                        [ Html.div [ class "setting-item-info" ]
                            [ Html.div [ class "setting-item-name" ]
                                [ Html.text "Include Untagged" ]
                            , Html.div [ class "setting-item-description" ]
                                [ Html.text "Whether to include a column for tasks with no tags" ]
                            ]
                        , Html.div [ class "setting-item-control" ]
                            [ Html.div
                                [ class <| "checkbox-container" ++ includeUntaggedStyle
                                , onClick ToggleIncludeUntagged
                                ]
                                []
                            ]
                        ]
                    , Html.div [ class "setting-item" ]
                        [ Html.div [ class "setting-item-info" ]
                            [ Html.div [ class "setting-item-name" ]
                                [ Html.text "Completed Count" ]
                            , Html.div [ class "setting-item-description" ]
                                [ Html.text "How many completed tasks to show.  Set to zero to disable the completed column altogether." ]
                            ]
                        , Html.div [ class "setting-item-control" ]
                            [ Html.input
                                [ type_ "text"
                                , value <| String.fromInt config.completedCount
                                , onInput EnteredCompletedCount
                                ]
                                []
                            ]
                        ]
                    , Html.div [ class "setting-item dialog-buttons" ]
                        [ Html.button
                            [ class "mod-warning"
                            , onClick <| DeleteBoardRequested
                            ]
                            [ Html.text "Delete this board"
                            ]
                        ]
                    ]
                ]

        Nothing ->
            Html.text ""


settingTitleSelectedView : Int -> BoardConfig -> Html Msg
settingTitleSelectedView index boardConfig =
    Html.div
        [ class "vertical-tab-nav-item is-active"
        , onClick <| SettingsBoardNameClicked index
        ]
        [ Html.text <| BoardConfig.title boardConfig ]


settingTitleView : Int -> BoardConfig -> Html Msg
settingTitleView index boardConfig =
    Html.div
        [ class "vertical-tab-nav-item"
        , onClick <| SettingsBoardNameClicked index
        ]
        [ Html.text <| BoardConfig.title boardConfig ]
